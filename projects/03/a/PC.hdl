// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16(in=regOutInternal,out=inInc16);
    Mux16(a=false,b=inInc16,sel=inc,out=incMux16);
    Mux16(a=incMux16,b=in,sel=load,out=loadMux16);
    Mux16(a=loadMux16,b=false,sel=reset,out=resetMux16);
    Or(a=reset,b=inc,out=resetOrInc);
    Or(a=resetOrInc,b=load,out=resetOrIncOrload);
    Register(in=resetMux16,load=resetOrIncOrload,out=out,out=regOutInternal);
}
